plugins { id "fabric-loom" version "0.9-SNAPSHOT" }

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-item-groups-v0",
        "fabric-item-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  apiModules.forEach {include(modImplementation(fabricApi.module(it, project.fabric_version)))}
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE.md") }